#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Long;
use File::Temp;
use Term::ANSIColor qw(:constants);

my ($LASTCHANGEDBY) = q$LastChangedBy: konganti $ =~ m/.+?\:(.+)/;
my ($LASTCHANGEDDATE) = q$LastChangedDate: 2013-10-09 12:46:11 -0500 (Wed, 09 Oct 2013) $ =~ m/.+?\:(.+)/;
my ($VERSION) = q$LastChangedRevision: 64 $ =~ m/.+?(\d+)/;
my $AUTHORFULLNAME = 'Kranti Konganti';

my ($help, $quiet, $setup, $get_uq_sc_opts,
    $fetch_sc_opts, $categorize_sc_opts,
    $cuffcmp_opts, $deps, $run, $out, $start_cpc);

my $is_valid_option = GetOptions('help:s'            => \$help,
                                 'quiet'             => \$quiet,
				 'setup'             => \$setup,
				 'run'               => \$run, 
				 'cuffcompare=s'     => \$cuffcmp_opts,
				 'out=s'             => \$out,
				 'cat-ncRNAs=s'      => \$categorize_sc_opts,
				 'fetch-seq=s'       => \$fetch_sc_opts,
				 'get-uq-feat=s'     => \$get_uq_sc_opts,
				 'cpc'               => \$start_cpc);

my $CAT_SC = 'categorize_ncRNAs.pl';
my $FETCH_SC = 'fetch_seq_from_ucsc.pl';
my $GET_UQ_SC = 'get_unique_features.pl';
my $CPC_SH_SC = 'CPC.sh';
my $USER_HOME = $ENV{'HOME'};

setup_ncRNAScan(`pwd`) if (defined $setup);
if (defined $run || (defined $help && $help eq '')) {
    run_ncRNAScan(`pwd`);
}
elsif (defined $help && $help ne '') {
    my $depconf_fh = read_conf('/.ncRNAScan.depconf');
    my $pl_dep_paths_fh = read_conf('/.ncRNAScan.PERLLIBS');
    my $pl_inc_string = source_env($pl_dep_paths_fh);
    get_deps($depconf_fh);
    system("$deps->{'cuffcompare'} -h") if ($help =~ m/cuff|cuffcompare/i);
    system("perl $pl_inc_string $deps->{'cat'} -h") if ($help =~ m/cat|cat-ncRNAs/i);
    system("perl $pl_inc_string $deps->{'get'} -h") if ($help =~ m/get|get-uq-feat/i);
    system("perl $pl_inc_string $deps->{'fetch'} -h") if ($help =~ m/fetch|fetch-seq/i);
    print "\nSee http://cpc.cbi.pku.edu.cn/docs/install_guide.jsp for help documentation.\n\n" if ($help =~ m/cpc/i);
    close $depconf_fh;
    close $pl_dep_paths_fh;
}
		

sub run_ncRNAScan {
    chomp(my $ran_fromHere = shift);
    my $outdir;

    $ran_fromHere .= '/' if ($ran_fromHere !~ m/\/$/);
    
    my $pl_dep_paths_fh = read_conf('/.ncRNAScan.PERLLIBS');
    my $pl_inc_string = source_env($pl_dep_paths_fh);

    # We will quit, if the setup was unsuccessful.
    require IO::Routine;
    IO::Routine->import();

    my $io = IO::Routine->new($help, $quiet);

    if (!defined $out) {
	$outdir = $io->validate_create_path($out, 'do not create',
					    'Directory path to ncRNAScan pipeline');
    }
    else {
	$outdir = $io->validate_create_path($out, 'create',
					    'Directory path to ncRNAScan pipeline')
    }

    my $s_time = $io->start_timer;
    $io->c_time('Starting ncRNAScan Pipeline...');

    my $dep_tools_fh = $io->open_file('<', $USER_HOME . '/.ncRNAScan.depconf');
    get_deps($dep_tools_fh);

    $io->c_time('Validating options...');
    $io->verify_options([$is_valid_option]);
    $io->exist_sys_cmd(['mkdir']);

    my $cuffcmp_dir = $outdir . 'cuffcompare';    

    # Run cuffcompare ################################################################################
    do {

	$io->c_time('Module 1: Running cuffcompare...');
	
	$io->execute_system_command("mkdir -p $cuffcmp_dir",
				    'Making output directory for cuffcompare [ ' . $cuffcmp_dir . ' ]')
	    if (!-d $cuffcmp_dir);
	
	$io->execute_system_command($deps->{'cuffcompare'} .
				    " -o $cuffcmp_dir/ncRNAScan_cuffcmp " .
				    join('-', split(/\-/, $cuffcmp_opts)),
				    "Command call:\n" .
				    "-------------\n" . $deps->{'cuffcompare'} .
				    " -o $cuffcmp_dir/ncRNAScan_cuffcmp " .
					join('-', split(/\-/, $cuffcmp_opts)));

    } if (defined($cuffcmp_opts) && $cuffcmp_opts ne '');
    

    # Run categorize_ncRNAs.pl ########################################################################
    do {

	$io->c_time('Module 2: Running categorize_ncRNAs.pl');
	
	my $is_failed = '';
	my $cat_dir = $outdir . 'categorize_ncRNAs';
	
	$io->execute_system_command("mkdir -p $cat_dir",
				    'Making output directory for ' .
				    $io->file_basename($deps->{'cat'}, 'suffix') .
				    ' [ ' . $cat_dir . ' ]')
	    if (!-d $cat_dir);
	
	if (defined $cuffcmp_opts && $cuffcmp_opts ne '') {
	    
	    my ($tr_files, $tr_size) = get_num_tr_files($cuffcmp_opts);
	    my $annot = get_annot($cuffcmp_opts);
	    
	    $io->c_time('Converting annotation file supplied with cuffcompare to gene predction format [ GTF -> genePred ]');
	    my $annot_for_cat = $cat_dir . '/' . $io->file_basename($annot) . '.txt';
	    
	    $is_failed = $io->execute_get_sys_cmd_output($deps->{'bin-gtfToGenePred'} . ' -genePredExt -geneNameAsName2 ' .
							 $annot . ' ' . $annot_for_cat,
							 "Command call:\n" .
							 "-------------\n" .
							 $deps->{'bin-gtfToGenePred'} . ' -genePredExt -geneNameAsName2 ' .
							 $annot . ' ' . $annot_for_cat);
	    
	     $is_failed = $io->execute_get_sys_cmd_output('perl' . $pl_inc_string . $deps->{'cat'} . ' ' .
							  join('-', split(/\-|\--/, $categorize_sc_opts)) .
							  " -cuffcmp $cuffcmp_dir/ncRNAScan_cuffcmp.tracking -out $cat_dir " .
							  "-bin $deps->{'bin-gtfToGenePred'} " . '-annot ' . $annot_for_cat . ' ' .
							  $tr_files, 
							  "Command call:\n" .
							  "-------------\n" . $deps->{'cat'} . ' ' . 
							  join('-', split(/\-|\--/, $categorize_sc_opts)) . 
							  " -cuffcmp $cuffcmp_dir/ncRNAScan_cuffcmp.tracking -out $cat_dir " .
							  "-bin $deps->{'bin-gtfToGenePred'} " . '-annot ' . $annot_for_cat . ' ' .
							  $tr_files);
	}
	else {
	    
	     $is_failed = $io->execute_get_sys_cmd_output('perl' . $pl_inc_string . $deps->{'cat'} . ' ' .
							  " -cuffcmp $cuffcmp_dir/ncRNAScan_cuffcmp.tracking -out $cat_dir " .
							  "-bin $deps->{'bin-gtfToGenePred'} " .
							  join('-', split(/\-|\--/, $categorize_sc_opts)),
							  "Command call:\n" .
							  "-------------\n" . $deps->{'cat'} . ' ' . 
							  " -cuffcmp $cuffcmp_dir/ncRNAScan_cuffcmp.tracking -out $cat_dir " .
							  "-bin $deps->{'bin-gtfToGenePred'} " .
							  join('-', split(/\-|\--/, $categorize_sc_opts)));
	}	
	pipeline_status($io, $is_failed);
	
    } if (defined($categorize_sc_opts) && $categorize_sc_opts ne '');

    # Run get_unique_features.pl ######################################################################
    do {

	$io->c_time('Module 3: Running get_unique_features.pl');
	
	my $is_failed = '';
        my $cat_dir = $outdir . 'get_unique_features';
        
	$io->execute_system_command("mkdir -p $cat_dir",
                                    'Making output directory for ' .
				    $io->file_basename($deps->{'get'}, 'suffix') .
				    ' [ ' . $cat_dir . ' ]')
            if (!-d $cat_dir);
	
	opendir (ncRNAScan_categorize_mod, $outdir . 'categorize_ncRNAs') ||
	    $io->error("Cannot open directory $outdir" .
		       'categorize_ncRNAs to read ncRNAScan class files.');
	    
	push my @ncRNAScan_class_files, grep {/putative\.class\.ncRNAs\.gtf$/} readdir ncRNAScan_categorize_mod;
	
	foreach my $ncRNAScan_class_file (@ncRNAScan_class_files) {
	    
	    $ncRNAScan_class_file = $outdir . 'categorize_ncRNAs/' . $ncRNAScan_class_file;
	    my $unique_ncRNAs = $cat_dir . '/' . $io->file_basename($ncRNAScan_class_file) . '.unique.gtf';
		
		 $is_failed = $io->execute_get_sys_cmd_output('perl' . $pl_inc_string . $deps->{'get'} . ' ' .
							      '-cf ' . $ncRNAScan_class_file . ' -cff gtf -sff bed '.
							      join('-', split(/\-|\--/, $get_uq_sc_opts)) .
							      ' -stdout -unique -kc 3 -ck transcript | ' .
							      "grep -oiP 'transcript_id\\s+.+?\\\;' | grep -oiP '\\\".+?\\\"' | sed -e 's/\\\"//g' | " .
							      "while read line; do grep \$line $ncRNAScan_class_file; done" .
							      " > $unique_ncRNAs",
							      "Command call:\n" .
							      "-------------\n" . $deps->{'get'} . ' ' .
							      '-cf ' . $ncRNAScan_class_file . ' -cff gtf -sff bed '.
							      join('-', split(/\-|\--/, $get_uq_sc_opts)) .
							      ' -stdout -unique -kc 3 -ck transcript ' .
							      " > $unique_ncRNAs");
	}
	pipeline_status($io, $is_failed);
	
	close ncRNAScan_categorize_mod;
		
    } if (defined($get_uq_sc_opts) && $get_uq_sc_opts ne '');
    
	
    # Run fetch_seq_from_ucsc.pl ######################################################################
    do {

	$io->c_time('Module 4: Running fetch_seq_from_ucsc.pl');
	
	my $is_failed = '';
        my $cat_dir = $outdir . 'fetch_seq_from_ucsc';
        
	$io->execute_system_command("mkdir -p $cat_dir",
                                    'Making output directory for ' . $io->file_basename($deps->{'get'}, 'suffix') . ' [ ' . $cat_dir . ' ]')
            if (!-d $cat_dir);
	
	opendir (ncRNAScan_get_unique_mod, $outdir . 'get_unique_features') ||
	    $io->error("Cannot open directory $outdir" .
		       'get_unique_features to read unique ncRNA features.');
	
	push my @ncRNAScan_unique_files, grep {/putative\.class\.ncRNAs\.unique\.gtf$/} readdir ncRNAScan_get_unique_mod;
	
	foreach my $ncRNAScan_unique_file (@ncRNAScan_unique_files) {
	    $ncRNAScan_unique_file = $outdir . 'get_unique_features/' . $ncRNAScan_unique_file;
	    
	    $is_failed = $io->execute_get_sys_cmd_output('perl' . $pl_inc_string . $deps->{'fetch'} . ' ' .
							 join('-', split(/\-|\--/, $fetch_sc_opts)) .
							 ' -tmap ' . $ncRNAScan_unique_file .
							 " -out $cat_dir -ff gtf -id 'transcript_id.+?\\\"(.+?)\\\"' -skip '\\texon\\t'",
							 "Command call:\n" .
							 "-------------\n" . $deps->{'fetch'} . ' ' .
							 join('-', split(/\-|\--/, $fetch_sc_opts)) .
							 ' -tmap ' . $ncRNAScan_unique_file .
							 " -out $cat_dir -ff gtf -id 'transcript_id.+?\\\"(.+?)\\\"' -skip '\\texon\\t'");
	}
	pipeline_status($io, $is_failed);
	
	close ncRNAScan_get_unique_mod;
	
    } if (defined($fetch_sc_opts) && $fetch_sc_opts ne '');
    
    # Run CPC ########################################################################################
    do {

	$io->c_time('Module 5: Running CPC.sh');
	
	my $ncRNAScan_final = $outdir . 'ncRNAScan.final';
        my $cat_dir = $outdir . 'CPC';

	$io->execute_system_command("mkdir -p $ncRNAScan_final",
                                    'Making output directory to store final list of ncRNAs [ ' . $io->file_basename($ncRNAScan_final) . '  ]')
	    if (!-d $ncRNAScan_final);

        $io->execute_system_command("mkdir -p $cat_dir",
                                    'Making output directory for ' . $io->file_basename($deps->{'cpc'}, 'suffix') . ' [ ' . $cat_dir . ' ]')
            if (!-d $cat_dir);
	
	opendir (ncRNAScan_fetch_seq_mod, $outdir . 'fetch_seq_from_ucsc') ||
	    $io->error("Cannot open directory $outdir" .
		       'fetch_seq_from_ucsc to read unique ncRNA features.');
	
	push my @ncRNAScan_unique_seq_files, grep {/putative\.class\.ncRNAs\.unique\.fa$/} readdir ncRNAScan_fetch_seq_mod;
	my $blastall_path = sub {
	    my @file_parts = $io->file_basename(shift, 'all');
	    chop $file_parts[1] if ($file_parts[1] =~ m/\/$/);
	    return $file_parts[1]
	};
	
	$io->exist_sys_cmd(['cut']);
	
	if (!exists $ENV{'CPC_HOME'}) {
	    $io->error("Environment variable CPC_HOME must be set pointing to CPC directory before running CPC.\n" .
		       'See CPC installation instructions (http://cpc.cbi.pku.edu.cn/docs/install_guide.jsp)');
	}
	
	foreach my $ncRNAScan_unique_seq_file (@ncRNAScan_unique_seq_files) {
	    $ncRNAScan_unique_seq_file = $outdir . 'fetch_seq_from_ucsc/' . $ncRNAScan_unique_seq_file;
	    my $CPC_out_file = $cat_dir . '/' . $io->file_basename($ncRNAScan_unique_seq_file) . '.CPC.predict.txt';
	    my $cpc_work_dir = $cat_dir . '/' . $io->file_basename($ncRNAScan_unique_seq_file) . '.CPC.out' ;
	    my $ncRNAScan_final_trs = $ncRNAScan_final . '/' . $io->file_basename($ncRNAScan_unique_seq_file) . '.final.gtf';
	    my $unique_gtf = $outdir . 'get_unique_features/' . $io->file_basename($ncRNAScan_unique_seq_file) . '.gtf';
	    
	    $io->execute_system_command("mkdir -p $cpc_work_dir");
	    
	    $io->execute_system_command($deps->{'cpc'} . 
					' "' . $ncRNAScan_unique_seq_file . ' ' . $CPC_out_file . ' ' . $cpc_work_dir . '" ' .
					$blastall_path->($deps->{'blastall'}),
					"Command call:\n" .
					"-------------\n" . $deps->{'cpc'} .
					' "' . $ncRNAScan_unique_seq_file . ' ' . $CPC_out_file . ' ' . $cpc_work_dir . '" ' .
					$blastall_path->($deps->{'blastall'}));
	    
	    if (-e $CPC_out_file && -s $CPC_out_file) {
		$io->execute_system_command("grep noncoding $CPC_out_file | cut -d \"|\" -f 1 | ".
					    "while read line; do grep \$line $unique_gtf; done > $ncRNAScan_final_trs",
					    'Getting noncoding "only" transcripts [ ' .
					    $io->file_basename($unique_gtf, 'suffix') .
					    " ] ...\n");
	    }
	    else {
		$io->error('CPC prediction step seems to have failed... Bailing out!');
	    }
	}
	close ncRNAScan_fetch_seq_mod;
    	
    } if (defined $start_cpc);

    close $dep_tools_fh;
    close $pl_dep_paths_fh;
    
    $io->c_time('ncRNAScan Pipeline Finished!');
    $io->end_timer($s_time);
}

sub setup_ncRNAScan {
    chomp(my $install_dir = shift);
    chomp(my $dl_util = `which wget 2>&1`);

    print "\nChecking for platform independent prerequisites on UNIX based machines...\n";

    # Wget or Curl
    if ($dl_util !~ m/.+?wget$/i) {
	chomp ($dl_util = `which curl 2>&1`);
	succ_or_fail(0, 'Curl|Wget')
	    if ($dl_util !~ m/.+?curl$/i);
	succ_or_fail(1, 'curl');
        $dl_util .= ' -LkO ';
    }
    elsif ($dl_util =~ m/.+?(wget)$/i) {
	succ_or_fail(1, $1);
    }
    else{
	succ_or_fail(0, 'Curl|Wget');
    }

    # Unzip
    check_util('unzip');

    # Make
    check_util('make');

    # Echo
    check_util('echo');

    # Uname
    check_util('uname');

    print "\n\nWe definetely need the custom module, IO::Routine.\n" .
      "We also need good grace of cpanm to install remaining modules.\n\n" .
	"Attempting to fetch IO::Routine from Perl-for-Bioinformatics repository on github...\n";

    if (-d "$install_dir/.build") {
	system("rm -rf $install_dir/.build");
    }

    system("mkdir $install_dir/.build");
    succ_or_fail(0, '.build dir') if (!-d "$install_dir/.build");

    system("rm $install_dir/master.zip") if (-e "$install_dir/master.zip");

    my $dl_util_slave = '';
    if ($dl_util =~ m/.*?wget$/i) {
      $dl_util_slave = $dl_util . ' -O master.zip ';
    }
    else {
      $dl_util_slave = $dl_util;
    }
    
    print "\n$dl_util_slave https://github.com/biocoder/Perl-for-Bioinformatics/archive/master.zip\n\n";
    system("$dl_util_slave https://github.com/biocoder/Perl-for-Bioinformatics/archive/master.zip > /dev/null 2>&1");
    system("mv $install_dir/master.zip $install_dir/.build/master.zip");

    succ_or_fail(0, 'master.zip')
	if (!-e "$install_dir/.build/master.zip");

    print "Inflating master.zip...\n";
    print "\nunzip -d $install_dir/.build $install_dir/.build/master.zip\n";
    system("unzip -d $install_dir/.build $install_dir/.build/master.zip  > /dev/null 2>&1");

    # Use cpanm to install Set::IntervalTree
    system("$dl_util https://raw.github.com/miyagawa/cpanminus/master/cpanm  > /dev/null 2>&1");
    system("mv cpanm $install_dir/.build");
    system("chmod 755 $install_dir/.build/cpanm");

    cpanm_status($install_dir, 'Module::Load');
    cpanm_status($install_dir, 'Set::IntervalTree');
    cpanm_status($install_dir, 'LWP::Simple');
    cpanm_status($install_dir, 'XML::XPath');
    cpanm_status($install_dir, 'XML::XPath::XMLParser');

    print "\n\nThank you cpanm!\n";

    my $pm_path = "$install_dir/PERLLIBS:$install_dir/PERLLIBS/lib:$install_dir/PERLLIBS/lib/perl5";

    print "\nInstalling IO::Routine...\n\n";
    system("mkdir $install_dir/PERLLIBS") if (!-d "$install_dir/PERLLIBS");
    my $custom_pm_log = `cd $install_dir/.build/Perl-for-Bioinformatics-master/IO-Routine;perl Makefile.PL PREFIX=$install_dir/PERLLIBS LIB=$install_dir/PERLLIBS/lib && make && make test && make install; 2>&1`;

    if ($custom_pm_log =~ m/fail|error|cannot/i) {
	succ_or_fail(0, 'IO::Routine');
    }
    else {
	succ_or_fail(1, 'IO::Routine');
    }

    require "$install_dir/PERLLIBS/lib/IO/Routine.pm";
    my $io = IO::Routine->new($help, $quiet);

    my @inst_file_parts = $io->file_basename($0, 'all');
    chop $inst_file_parts[1] if ($inst_file_parts[1] =~ m/\/$/);

    if (!-d "$inst_file_parts[1]/.ncRNAScan.depbin") {
	print "\nMoving required tools and scripts to current install path...\n";
	system("mkdir $install_dir/.ncRNAScan.depbin");
	system("mv $install_dir/.build/Perl-for-Bioinformatics-master/NGS-Utils/.ncRNAScan.depbin/* $install_dir/.ncRNAScan.depbin/.");
	system("mv $install_dir/.build/Perl-for-Bioinformatics-master/NGS-Utils/* $install_dir/.");
    }
    
    print "\nCleaning up build directory...\n";
    system("rm -rf $install_dir/.build");

    $io->execute_system_command(0, 'Detecting system architecture...');

    my $sys_arch_info = $io->execute_get_sys_cmd_output('uname -a');

    $io->error('This is not a UNIX based machine ... Aborting installation!')
	if (!$sys_arch_info ||
	    $sys_arch_info !~ m/linux|darwin/i);

    $io->error('It is not a 64-bit machine!' . 
	       "\nIt is your responsibility to make sure that you have the following tools" .
	       " installed for your system architecture and also must be found in \$PATH:\n" .
	       "\nblastall, cuffcompare and RNAfold\n\nBailing out!") 
	if ($sys_arch_info !~ m/x86\_64/i);


    my $sys_arch = '';
    if ($sys_arch_info =~ m/darwin/i) {
	$sys_arch = 'darwin';
	$io->execute_system_command(0, 'Skipping version requirement check for system level commands [ tar ], [ cut ] and [ wc ].' .
				    "\nFreeBSD's tools does not provide version numbers (?)");
	check_util('tar');
	check_util('cut');
	check_util('wc');

	# Darwin special case...
	chomp(my $darwin_sed = `sed --version 2>&1`);
	chomp(my $darwin_grep = `grep --version 2>&1`);

	if ($darwin_sed !~ m/gnu/i || $darwin_grep !~ m/gnu/i) {
	    print "\n";
	    $io->warning("We need GNU's grep and sed instead of FreeBSD's.");
	    chomp(my $homebrew = `which brew 2>&1`);
	    print "Looking to see if you have homebrew to install GNU tools [ grep and sed ] ...\n";
	    
	    if ($homebrew =~ m/.*brew$/i) {
		succ_or_fail(1, 'homebrew');
	    }
	    else {
		$io->error('Please install homebrew. See installation instructions at http://brew.sh/' . 
			   "\nThen, do the following:\n\nbrew update\nbrew tap homebrew/dupes\n" .
			   "brew install grep\nbrew install gnu-sed\n" .
			   "ln -s /usr/local/bin/ggrep /usr/local/bin/grep\n" .
			   "ln -s /usr/local/bin/gsed /usr/local/bin/sed\n\n" .
			   "Then, rerun the setup procedure to successfully install ncRNAScan pipeline.");
	    }
	    print "\n\nPlease execute the following commands in order and" .
		" rerun the setup procedure to successfully install ncRNAScan pipeline.\n";
	    $io->execute_system_command(0,
					"brew update\nbrew tap homebrew/dupes\n" .
					"brew install grep\nbrew install gnu-sed\n" .
					"ln -s /usr/local/bin/ggrep /usr/local/bin/grep\n" .
					"ln -s /usr/local/bin/gsed /usr/local/bin/sed");
	    print "Skipping version check for GNU grep and GNU sed...\n";
	    succ_or_fail(2, 'sed');
	    succ_or_fail(2, 'grep');
	    print "\n\nAborting setup...\n\n";
	    exit;
	}
	else {
	    $io->check_sys_level_cmds(['grep', 'sed'],
				      ['2.6.3', '4.2.1']);
	    succ_or_fail(1, 'grep');
	    succ_or_fail(1, 'sed');
	}
    }
    else {
	$sys_arch = 'linux';
	 $io->check_sys_level_cmds(['grep', 'sed', 'tar', 'cut', 'wc'],
				   ['2.6.3', '4.2.1', '0', '8', '8']);

	succ_or_fail(1, 'grep');
	succ_or_fail(1, 'sed');
	succ_or_fail(1, 'tar');
	succ_or_fail(1, 'cut');
	succ_or_fail(1, 'wc');
    }

    print "\n\nSetting up PERL5LIB paths...\n";
    unlink "$USER_HOME/.ncRNAScan.PERLLIBS" if (-e "$USER_HOME/.ncRNAScan.PERLLIBS");
    my $pl_dep_fh = $io->open_file('>', "$USER_HOME/.ncRNAScan.PERLLIBS");
    print $pl_dep_fh $pm_path;

    $io->execute_system_command(0,
				"\nChecking for ncRNAScan pipeline tool dependencies..." .
				"\n\nWriting tool dependency chain to $USER_HOME/.ncRNAScan.depconf");

    my $dep_fh = $io->open_file('>', "$USER_HOME/.ncRNAScan.depconf");

    # We will figure out if user is installing at a different location other than
    # from the cloned repo.
    $install_dir = $inst_file_parts[1] if (-d "$inst_file_parts[1]/.ncRNAScan.depbin" && $inst_file_parts[1] !~ m/^\./);

    print $dep_fh check_bio_util('cuffcompare', $install_dir, $sys_arch), "\n";
    print $dep_fh check_bio_util('blastall', $install_dir, $sys_arch), "\n";
    print $dep_fh check_bio_util('RNAfold', $install_dir, $sys_arch), "\n";
    print $dep_fh check_bio_util('gtfToGenePred', $install_dir, $sys_arch), "\n";
    print $dep_fh "$install_dir/" . $CAT_SC, "\n" if (check_native("$install_dir/" . $CAT_SC));
    print $dep_fh "$install_dir/" . $FETCH_SC, "\n" if (check_native("$install_dir/" . $FETCH_SC));
    print $dep_fh "$install_dir/" . $GET_UQ_SC, "\n" if (check_native("$install_dir/" . $GET_UQ_SC));
    print $dep_fh "$install_dir/" . $CPC_SH_SC, "\n" if (check_native("$install_dir/" . $CPC_SH_SC));
    if (!exists $ENV{'CPC_HOME'}) {
	succ_or_fail(3, 'CPC');
	print "\n\nSeems like Coding Potential Calculator (CPC) is not installed. This may not be a problem if you do not intend to use CPC.";
	print "\nSee CPC installation instructions (http://cpc.cbi.pku.edu.cn/docs/install_guide.jsp) if you would like to run CPC module with ncRNAScan.";
    }
    close $dep_fh;
    $io->execute_system_command(0,
				"\n\nSetup complete. See \"perl ncRNAScan -h\" to run the pipeline with options.");
    # For build check
    exit;
}


# Check for pipeline scripts.
sub check_native {
    my $nat_sc = shift;
    (my $sc) = ($nat_sc =~ m/.*\/(.+?\.pl|.+?\.sh)$/);

    if (-e $nat_sc) {
	succ_or_fail(1, $sc);
	return 1;
    }
    else {
	succ_or_fail(0, $sc);
    }
    return 0;
}

# Check for dependencies
sub check_bio_util {
    my $cmd = shift;
    my $install_dir = shift;
    my $sys_arch = shift;

    chomp (my $biocmd4arch = `which $cmd 2>&1`);
    my $sandboxed4arch = $sys_arch . '_' . $cmd;

    if ($biocmd4arch !~ m/.*$cmd$/i &&
        -d "$install_dir/.ncRNAScan.depbin" &&
        -e "$install_dir/.ncRNAScan.depbin/$sandboxed4arch") {
        succ_or_fail(1, $cmd);
	$biocmd4arch = "$install_dir/.ncRNAScan.depbin/$sandboxed4arch";
    }
    elsif ($biocmd4arch =~ m/.*$cmd$/i) {
	succ_or_fail(1, $cmd);
    }
    else {
	succ_or_fail(0, $cmd);
    }
    return $biocmd4arch;
}

# Check for system utilities
sub check_util {
    my $cmd = shift;
    chomp (my $util = `which $cmd 2>&1`);

    if ($util !~ m/.+?$cmd/i) {
	succ_or_fail(0, $cmd);
	print "\nAborting...\n\n";
	exit;
    }
    else {
        succ_or_fail(1, $cmd);
    }
    return $util;
}

# print success or failure of ncRNAScan pipeline modules
sub pipeline_status {
    my $self = shift;
    my $status = shift;
    print $status;
    $self->c_time('ncRNAScan pipeline aborted!');
    die "\n" if ($status =~ m/ERROR!/);
    return;
}

# Print success or failure
sub succ_or_fail {
    my $code = shift;
    my $msg = shift;

    my $char_white_space = 70 - length($msg);

    print "\n";
    printf("%s%*s%s", $msg, $char_white_space, '... ', BOLD GREEN . 'OK' . RESET)
	if ($code == 1);

    if ($code == 0) {
	printf("%s%*s%s", $msg, $char_white_space, '... ',  BOLD RED . 'FAIL' . RESET);
	print "\n\n";
	exit;
    }

    printf("%s%*s%s", $msg, $char_white_space, '... ', BOLD YELLOW . 'SKIP' . RESET)
         if ($code == 2);

    printf("%s%*s%s", $msg, $char_white_space, '... ', BOLD YELLOW . 'WARN' . RESET)
	if ($code == 3);

    return;
}

# Source the environment
sub source_env {
    chomp(my $pl_dep_paths_fh = shift);
    chomp(my $pl_paths = do { local $/; <$pl_dep_paths_fh> });

    my $perl_inc_string = ' ';
    
    foreach my $pl_path (split(/\:/, $pl_paths)) {
	$perl_inc_string .= '-I' . $pl_path . ' ';
	push @INC, $pl_path;
    }
    return $perl_inc_string;
}

# Report cpanm status
sub cpanm_status {
    my $install_dir = shift;
    my $pm = shift;
    my $cpanm_log = `$install_dir/.build/cpanm -f -l $install_dir/PERLLIBS $pm 2>&1`;

    succ_or_fail(0, $pm) if ($cpanm_log !~ m/.*?success.*/i);
    succ_or_fail(1, $pm);
    return;
}

# Report transcript files and size
sub get_num_tr_files {
    my $cmd_args = shift;
    my @args = split/\s+/, $cmd_args;
   
    my $last_opt_elem = my $tr_size = 0;
    my $tr_files = '';
    
    for (0 .. $#args) {
	$last_opt_elem = $_ + 2 if ($args[$_] =~ m/^\-/);
    }
    $tr_size = $#args - $last_opt_elem;
    
    $tr_files = join(' ', @args[$last_opt_elem .. $#args]);
    return($tr_files, $tr_size);
}

# Get cuffcompare's annotation file
sub get_annot {
    my $cmd_args = shift;
    my $annot_file = '';
    ($annot_file) = ($cmd_args =~ m/.*?-r\s+(.+?)\s+.*/i);
    return $annot_file;
}

# Ask for user input
sub ask_user {
 my $msg = shift;
 print $msg, ': ';
 chomp(my $ans = <STDIN>);
 return $ans;
}

# Get deps
sub get_deps {
    my $dep_tools_fh = shift;
    my $dep_tools = do {local $/; <$dep_tools_fh>};
    ($deps->{'cuffcompare'}) = ($dep_tools =~ m/(.+?cuffcompare)/i);
    ($deps->{'blastall'}) = ($dep_tools =~ m/(.+?blastall)/i);
    ($deps->{'RNAfold'}) = ($dep_tools =~ m/(.+?rnafold)/i);
    ($deps->{'bin-gtfToGenePred'}) = ($dep_tools =~ m/(.+?gtfToGenePred)/i);
    ($deps->{'cat'}) = ($dep_tools =~ m/(.+?$CAT_SC)/i);
    ($deps->{'fetch'}) = ($dep_tools =~ m/(.+?$FETCH_SC)/i);
    ($deps->{'get'}) = ($dep_tools =~ m/(.+?$GET_UQ_SC)/i);
    ($deps->{'cpc'}) = ($dep_tools =~ m/(.+?$CPC_SH_SC)/i);
    return;
}

# Read ncRNAScan conf
sub read_conf {
    my $conf = shift;
    open(my $conf_fh, '<', $USER_HOME . $conf) ||
        die "\nCannot open $USER_HOME$conf" . ".\nMay be setup was unsuccessful (?): $!\!\n" .
        "\nRun \"perl ncRNAScan -setup\" to try to setup ncRNAScan pipeline.\n\n";
    return $conf_fh;
}

__END__

=head1 NAME

ncRNAScan

=head1 SYNOPSIS

ncRNAScan is a pipeline to extract putative novel ncRNAs ab initio, given a list of transcripts in GTF format assembled from deep sequencing data (ex: RNA-Seq).

Complete Description: 

    perldoc ncRNAScan

Examples:

    perl ncRNAScan -h

    perl ncRNAScan -run -out /data/ncRNAScan/ -cuffcmp '-r annotation.gtf -s genome.fa transcripts1.gtf transscripts2.gtf' -cat-ncRNAs '-fpkm 2 -l 200 -min-exons 2 -antisense' -get-uq-feat '-sf known_ncRNAs.bed' -fetch-seq '-db mm10' -cpc


=head1 DESCRIPTION

This pipeline script will bind together the functionality of the tools / scripts: Cuffcompare, categorize_ncRNAs.pl, get_unique_features.pl, fetch_seq_from_ucsc.pl and / or Coding Potential Calculator (CPC). Transcriptome construction tools such as Cufflinks produces a set of assembled transcripts in GTF format. ncRNAScan uses this data in addition to known gene annotation to extract putative ncRNAs constructed by the ab initio assemblers. In brief, the pipeline steps are as follows:

=over 5

=item 1. Cuffcompare (ncRNAScan Option: -cuff or --cuffcompare)

    The transcript assembly can be compared to known annotation of choice to classify them into different class codes (http://cufflinks.cbcb.umd.edu/manual.html#class_codes) using Cuffcompare.

    To get help documentation about this module, run: perl ncRNAScan -h cuff

=item 2. categorize_ncRNAs.pl (ncRNAScan Option: -cat or --cat-ncRNAs)
    
    ncRNAScan uses the tracking file (*.tracking) produced by Cuffcompare, annotation data in gene prediction format and a list of supplied transcripts in GTF format to produce and categorize ncRNAs into different classes as mentioned in the paper, http://genome.cshlp.org/content/22/3/577.full. 

    To get help documentation about this module, run: perl ncRNAScan -h cat

=item 3. get_unique_features.pl (ncRNAScan Option: -get or --get--uq-feat)
    
    It then compares the putative list with supplied known ncRNAs in BED format to get features that do not overlap any known ncRNAs.

    To get help documentation about this module, run: perl ncRNAScan -h get

=item 4. fetch_seq_from_ucsc.pl (ncRNAScan Option: -fetch or --fetch-seq)
    
    This list is then used to fetch DNA sequence of those transcript sequences to determine the coding potential using CPC.
    
    To get help documentation about this module, run: perl ncRNAScan -h fetch

=item 5. CPC.sh (ncRNAScan Option -cpc or --cpc)
    
    In the final step, the fetched FASTA sequences are used to determine the coding potential and those that are flagged as "noncoding" are used to create the final list of high confidence ncRNAs.

    To get help documentation about this module, run: perl ncRNAScan -h cpc

=back

The final output is a putative list of ncRNAs in GTF format that can be directly uploaded as custom tracks to Genome Browsers, such as UCSC etc...

The pipeline has the ability to run each of these individual modules if any of them fail during the initial run.

=head1 EXAMPLE

=head2 Installation:

=over 5

=item * This script will try its best to setup the pipeline on the suitable architecture.

perl ncRNAScan -setup

=back

=head2 Start the Pipeline:

=over 5

=item * Run the complete pipeline:

    perl ncRNAScan -run -out /data/ncRNAScan/ -cuff '-r /data/projects/mm10/macrophages/rna-seq/cuffcompare.m0.m1.m2_vs_ensembl_ref_known/refSeq_UCSCKnownGenes_Ensemble.gtf -s /data/ref_fasta/mm10_UCSC/whole_genome.fa /data/projects/mm10/macrophages/rna-seq/m0/cufflinks/transcripts.gtf /data/projects/mm10/macrophages/rna-seq/m1/cufflinks/transcripts.gtf /data/projects/mm10/macrophages/rna-seq/m2/cufflinks/transcripts.gtf' -cat '-clean -overlap 1.1 -min-exons 1 -fpkm 2 -antisense -sample-names "M0,M1,M2"' -get '-sf /data/projects/mm10/macrophages/ncrna/known_ref_ncRNAs/mm10_ncRNA.bed' -fetch '-db mm10' -cpc

=item * Run categorize_ncRNAs module of the pipeline:
    
    perl ncRNAScan -run -out /data/ncRNAScan/ -cat '-clean -overlap 1.1 -min-exons 1 -fpkm 2 -annotation /data/projects/mm10/macrophages/ncrna/2lncRNA/allGenes.txt -antisense -sample-names "M0,M1,M2" /data/projects/mm10/macrophages/rna-seq/m0/cufflinks/transcripts.gtf /data/projects/mm10/macrophages/rna-seq/m1/cufflinks\
/transcripts.gtf /data/projects/mm10/macrophages/rna-seq/m2/cufflinks/transcripts.gtf'

=item * Run get_unique_features module of the pipeline:
    
    perl ncRNAScan -run -out /data/ncRNAScan -get '-sf /data/projects/mm10/macrophages/ncrna/known_ref_ncRNAs/mm10_ncRNA.bed'

=item * Run fetch_unique_features module of the pipeline:

    perl ncRNAScan -run -out /data/ncRNAScan -fetch '-db mm10'

=item * Run CPC module of the pipeline:

    perl ncRNAScan -cpc

=back

=head1 OPTIONS

ncRNAScan takes the following arguments:

=over 4

=item -h or --help (Optional)
    
    Displays this helpful message.

=item -setup or --setup
    
    Try to setup the pipeline with all its dependencies.

=item -cuff or --cuffcompare (Optional)

    Run the Cuffcompare module with supplied options.

=item -cat or --cat-ncRNAs (Optional)

    Run the categorize_ncRNAs.pl module with supplied options.

=item -get or --get-uq-feat (Optional)

    Run the get_unique_features.pl module with supplied options.

=item -fetch or --fetch-seq (Optional)

    Run the fetch_seq_from_ucsc.pl module with supplied options.

=item -cpc or --cpc (Optional)

    Run the CPC module. No options are required.

=back

=head1 AUTHOR

Kranti Konganti, E<lt>konganti@tamu.eduE<gt>.

=head1 COPYRIGHT

This program is distributed under the Artistic License.

=head1 DATE

Oct-30-2013

=cut
